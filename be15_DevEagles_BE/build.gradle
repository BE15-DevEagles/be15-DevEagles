plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id "com.diffplug.spotless" version "6.20.0"
}

spotless {
    java {
        importOrder('java', 'javax', 'org', 'com', '')
        removeUnusedImports()
        googleJavaFormat('1.27.0')
        formatAnnotations()
    }
}

group = 'com.deveagles'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('updateGitHooks', Copy) {
    from file('scripts/pre-commit')          // 커밋 훅 원본 위치
    into file('.git/hooks')                  // Git 훅 설치 위치
}

tasks.register('makeGitHooksExecutable') {
    dependsOn updateGitHooks

    doLast {
        def os = System.getProperty("os.name").toLowerCase()
        if (!os.contains("windows")) {
            println 'Unix OS: chmod executed'
            exec {
                commandLine 'chmod', '+x', '.git/hooks/pre-commit'
            }
        } else {
            println 'Windows OS: chmod skipped'
        }
    }
}

tasks.named('compileJava') {
    dependsOn makeGitHooksExecutable
}
